
#Область ОписаниеПеременных

#КонецОбласти

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	КопироватьДанныеФормы(Параметры.Ключ, Объект);
	Элементы.РазрешитьMergeConflict.Видимость = Объект.СписокКонфликтов.Количество() > 0;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "сп_ПрочитатьЛогФайла" Тогда
		
		ПрочитатьЛогФайл();
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормы

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура РазрешитьMergeConflict(Команда)
	
	ПараметрыОткрытия = Новый Структура;
	ПараметрыОткрытия.Вставить("Ключ", Объект);
	
	ОткрытьФорму("Обработка.сп_ВыполнениеСценариев.Форма.ФормаРазрешенияКонфликтов", ПараметрыОткрытия, 
		ЭтаФорма, Новый УникальныйИдентификатор, , , , 
		РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
		
КонецПроцедуры

&НаКлиенте
Процедура ЗакрытьФорму(Команда)
	
	Результат = Новый Структура;
	Результат.Вставить("ПрерватьВыполнениеКоманд", Элементы.ГруппаДекорации.Видимость = Истина);
	
	Закрыть(Результат);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ПрочитатьЛогФайл() 
	
	ЛогФайл = Новый Файл(Объект.ЛогФайл);
	
	Если Не ЛогФайл.Существует() Тогда
		Возврат;
	КонецЕсли;

	КоличествоСтрок = ДанныеЛогФайла.КоличествоСтрок();
	
	ЧтениеТекста = Новый ЧтениеТекста;
	ЧтениеТекста.Открыть(ЛогФайл.ПолноеИмя, КодировкаТекста.UTF8, , , Ложь);
	
	Пока Истина Цикл
		СтрокаТекста = ЧтениеТекста.ПрочитатьСтроку();
		
		Если КоличествоСтрок > 0 Тогда
			КоличествоСтрок = КоличествоСтрок - 1;
			Продолжить;
		КонецЕсли;
		
		Если СтрокаТекста = Неопределено Тогда
		    Элементы.ГруппаДекорации.Видимость = Истина;
			Прервать;
		ИначеЕсли СтрокаТекста <> Неопределено И СтрНайти(СтрокаТекста, "конец выпонения сценария") <> 0 Тогда 
			ДанныеЛогФайла.ВставитьСтроку(0, СтрокаТекста);
		    Элементы.ГруппаДекорации.Видимость = Ложь;
			Прервать;
		КонецЕсли;
		
		ДанныеЛогФайла.ВставитьСтроку(0, СтрокаТекста);
	КонецЦикла;
	
	ЧтениеТекста.Закрыть();	
	
	// найти файлы с merge conflict
	Объект.СписокКонфликтов.Очистить();

	МассивКонфликтов = сп_ОбщегоНазначенияКлиент.МассивКонфликтов(ДанныеЛогФайла.ПолучитьТекст());

	Для Каждого Конфликт Из МассивКонфликтов Цикл
		НовСтрока = Объект.СписокКонфликтов.Добавить();
		НовСтрока.ПолноеИмяФайла = Конфликт;		
	КонецЦикла;

	Элементы.РазрешитьMergeConflict.Видимость = Объект.СписокКонфликтов.Количество() > 0;
КонецПроцедуры

#КонецОбласти